{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "##### The latest version of this Jupyter notebook is available at [http://github.com/jckantor/CBE40455](http://github.com/jckantor/CBE20255)  for noncommercial use under terms of the [Creative Commons Attribution Noncommericial ShareAlike License](http://creativecommons.org/licenses/by-nc-sa/4.0/).\n",
    "\n",
    "J.C. Kantor (Kantor.1@nd.edu)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Data Reconciliation"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "This notebook demonstrates use of linear programming using GLPK/Mathprog for reconciliation of process measurements."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Data Reconciliation and Gross Error Detection"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The following demonstration was motivated by a former student Matt N. who was exploring methods for assessing measurement information in a refinery application. The illustration was adapted from Chapter 1 of \"Reconciliation & Gross Error Detection: An Intelligent Use of Process Data\" by Shankar Narasimhan and Cornelius Jordache (Gulf Publishing, 2000)."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Heat Exchanger with Bypass\n",
    "<pre>\n",
    "           1/1          2/2         4/3         6/5\n",
    "        -------- SPLIT ----- HTEXG ----- MIXER --------\n",
    "                   |                       ^ \n",
    "                   |                       |\n",
    "                   |    3            5/4   |\n",
    "                   +-------> VALVE --------+\n",
    "</pre>\n",
    "\n",
    "The flow sheet is labeled by `s/k` pairs which refer to stream `s` and measurement `k`. Streams are numbered `s` in `1..S` and sensors are numbered `k` in `1..K`. The parameter `s[k]` denotes the stream associated with measurement `k`.\n",
    "\n",
    "Given sensor measurements $y_k$, the task is to infer values for the flowrates $x_{s_k}$ using mass balance equations and a sensor model\n",
    "\n",
    "$$\\begin{eqnarray}\n",
    "     |y_k-x_{s_k}| & \\leq & e_k + M g_k \\\\\n",
    "     |e_k| & \\leq & e^{max}_k\n",
    "\\end{eqnarray}$$\n",
    "\n",
    "$g_k$ is a binary variable indicating the presence of a gross error in sensor $k$. $M$ is a big number. A gross error occurs if the measured data is not consistent with sensor model and mass balances. The optimization objective is to find an estimate of mass flows minimizing the number of gross sensor errors."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Writing DataReconciliation.dat\n"
     ]
    }
   ],
   "source": [
    "%%writefile DataReconciliation.dat\n",
    "\n",
    "/* All of the relevant sensor information is given in the data section. The\n",
    "relevant information includes the number of sensors, and a table indexed by \n",
    "sensor number k with information on which stream is measured (s[k]), the \n",
    "measurement (y[k]), and  'no-penalty' range for measurement errors (emax[k]). \n",
    "*/\n",
    "\n",
    "set UNITS := SPLIT HTEXG VALVE MIXER;\n",
    "set STREAMS := 1 2 3 4 5 6;\n",
    "\n",
    "/* Mass Balance Coefficients */\n",
    "param  A :\n",
    "            1   2   3   4   5   6  :=\n",
    "   SPLIT   -1   1   1   .   .   .\n",
    "   HTEXG    .  -1   .   1   .   .\n",
    "   VALVE    .   .  -1   .   1   .\n",
    "   MIXER    .   .   .   1   1  -1 ;\n",
    "\n",
    "/* Sensors and Measurement Data*/\n",
    "param: SENSORS:   s      y     emax :=    \n",
    "          1       1   101.91   1.75\n",
    "          2       2    68.45   1.75\n",
    "          3       4    64.20   1.75\n",
    "          4       5    36.44   1.75\n",
    "          5       6    98.88   1.75 ;\n",
    "\n",
    "end;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": [
    "%%script glpsol -m /dev/stdin -d DataReconciliation.dat --out output\n",
    "\n",
    "# Example: DataReconciliation.mod\n",
    "\n",
    "/* Sets */\n",
    "set STREAMS;\n",
    "set SENSORS;\n",
    "set UNITS;\n",
    "\n",
    "/* Mass Balances  Coefficients */\n",
    "param A{UNITS,STREAMS} default 0;\n",
    "\n",
    "/* Sensors Parameters */\n",
    "param y{SENSORS} >= 0;        # Measurement Data\n",
    "param s{SENSORS}  > 0;        # Stream measured by each sensor\n",
    "param emax{SENSORS} >= 0;     # Bound on error for each sensor\n",
    "param bigM := 100;            # Big M\n",
    "\n",
    "/* Stream Variables */\n",
    "var x{STREAMS} >= 0;\n",
    "\n",
    "/* Sensor Variables */\n",
    "var epos{SENSORS} >= 0;\n",
    "var eneg{SENSORS} >= 0;\n",
    "var gerr{SENSORS} binary;     # Gross Errors\n",
    "\n",
    "/* Optimization Criterion */\n",
    "minimize L1: sum {k in SENSORS} (epos[k]+eneg[k] + bigM*gerr[k]);\n",
    "\n",
    "/* Mass Balances */\n",
    "s.t. MASSBAL {i in UNITS}: sum{j in STREAMS} A[i,j]*x[j] = 0;\n",
    "\n",
    "/* Error Model */\n",
    "s.t. s_err {k in SENSORS}: y[k] =  x[s[k]] + epos[k] - eneg[k];\n",
    "s.t. e_pos {k in SENSORS}: epos[k] <= emax[k] + bigM*gerr[k];\n",
    "s.t. e_neg {k in SENSORS}: eneg[k] <= emax[k] + bigM*gerr[k];\n",
    "\n",
    "/* Solve and Display Solution */\n",
    "solve;\n",
    "\n",
    "printf \"Measurement Reconciliation\";\n",
    "printf \"\\n\\n   Sensor    Measured     Reconciled    Difference    GrossError\";\n",
    "for {k in SENSORS}{\n",
    "   printf \"\\n      %s      %7.2f       %7.2f      %7.2f\",\n",
    "      k,y[k],x[s[k]],(epos[k]-eneg[k]),gerr[k];\n",
    "   printf \"         %3s\", if gerr[k]=1 then \"X\" else \" \";\n",
    "}\n",
    "printf \"\\n\\n\\nStream Estimates\";\n",
    "printf \"\\n\\n   Stream    Estimate\";\n",
    "printf {i in STREAMS} \"\\n      %s      %7.2f\",i,x[i];\n",
    "\n",
    "table tab1 {k in SENSORS} OUT \"CSV\" \"MeasurementReconciliation.csv\" \"ColumnChart\":\n",
    "    k~Sensor,\n",
    "    y[k]~Measurement,\n",
    "    x[s[k]]~Reconciled;\n",
    "\n",
    "table tab2 {k in SENSORS} OUT \"CSV\" \"MeasurementError.csv\" \"ColumnChart\":\n",
    "    k~Sensor,\n",
    "    (epos[k]-eneg[k])~Error;\n",
    "\n",
    "data;\n",
    "\n",
    "end;\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Visualization"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "metadata": {
    "collapsed": false
   },
   "outputs": [
    {
     "data": {
      "text/html": [
       "<div>\n",
       "<table border=\"1\" class=\"dataframe\">\n",
       "  <thead>\n",
       "    <tr style=\"text-align: right;\">\n",
       "      <th></th>\n",
       "      <th>Sensor</th>\n",
       "      <th>Error</th>\n",
       "    </tr>\n",
       "  </thead>\n",
       "  <tbody>\n",
       "    <tr>\n",
       "      <th>0</th>\n",
       "      <td>1</td>\n",
       "      <td>1.28</td>\n",
       "    </tr>\n",
       "    <tr>\n",
       "      <th>1</th>\n",
       "      <td>2</td>\n",
       "      <td>4.25</td>\n",
       "    </tr>\n",
       "    <tr>\n",
       "      <th>2</th>\n",
       "      <td>3</td>\n",
       "      <td>0.00</td>\n",
       "    </tr>\n",
       "    <tr>\n",
       "      <th>3</th>\n",
       "      <td>4</td>\n",
       "      <td>0.01</td>\n",
       "    </tr>\n",
       "    <tr>\n",
       "      <th>4</th>\n",
       "      <td>5</td>\n",
       "      <td>-1.75</td>\n",
       "    </tr>\n",
       "  </tbody>\n",
       "</table>\n",
       "</div>"
      ],
      "text/plain": [
       "   Sensor  Error\n",
       "0       1   1.28\n",
       "1       2   4.25\n",
       "2       3   0.00\n",
       "3       4   0.01\n",
       "4       5  -1.75"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    },
    {
     "data": {
      "image/png": "iVBORw0KGgoAAAANSUhEUgAAAXwAAAEUCAYAAAAoQI39AAAABHNCSVQICAgIfAhkiAAAAAlwSFlz\nAAALEgAACxIB0t1+/AAAGURJREFUeJzt3X2UbXV93/H353JBQIQxEsOjDiayFIoOPvAQaxhNNaRB\nMWvVWlsSx6b2IQ+aoGiUpOJaVuyKLaSkto2ajA8N4hKoXitWaNgUil4l3lEeLhQfrhJEiCDIs8B8\n+8fZw28798ycmcuZ/fvd8/u81jrrnr3PPud8z5fNd/b5nDN7FBGYmdnk25S7ADMz64cHvplZJTzw\nzcwq4YFvZlYJD3wzs0p44JuZVcID38ysEh74NhaSdkh6WNLTlq3fJmlR0jNy1Vaytm8vX+X22bZ/\n9y67HN9nnTYZPPBtXAL4NvD6pRWSjgH2aW8rglq56+gIYFQ9t0bEU5Zdti7faNhrk7R5PcWsd3vb\nvXjg2zh9AvjNzvIbgI/RGWiSniTpA5K+K+kHkv6LpL3b26YkfU7SHZLukrRF0qGd+85J+pakH0v6\ntqR/2q4/S9LHO9tNt0fFm9rlRtJ7Jf1f4H7gCEnPkXSppDsl3SjptZ37z0v6oKTPt0fTV0n6OUnn\ntnVtlzTT2f4QSRe2dX9b0u91bjtL0qckfbSt+zpJL2xv+zjwDGBL+zxvW2/Dl722+4Bnta/9tyXd\nDNzUbvcmSTe3r/czkg7uPMaw7c+RdLukeyR9Q9LR663NyuOBb+P0ZWD/dpjuAbyOwQ+BrvcDvwA8\nv/33UODftrdtAj7CYAg+A3gQ+DMASU8G/hQ4OSL2B04EFtr7reUdxGnAvwD2A+4ELm1r+1ngnwAf\nlPTczvavBc4EDgQeBr4EXAM8Dfg08B/bujYBW4BtwCHALwO/L+mVncd6FXA+cADw2aXXFBG/AXwP\nOKU9av/AGl7Haq/tKe3jAZwKvBg4qo2M3te+poOB7wKfXPYY3e1/BXgp8OyIOKC93527WJsVxAPf\nxu3jDI7yXwHcANy6dEMbN7wJOD0i7o6I+4CzGQxcIuKuiLg4Ih5qb3sfcFLnsReBYyTtExG3R8QN\nSw89oqYA5iNie0QsAicD34mIj0bEYkQsABcxGGxLLoqIbRHxMHAx8GBEfCIGJ5/6FHBsu92LgQMj\n4r0R8WhEfAf48NJral0ZEV9o7/sJBj/s1uMQST/qXO6StM+w1xYRj7Trz257/DDwz4CPRMRCRPwE\neCdw4rLPVbrb/4TBD4/nStoUETdFxA/WWbMVyHmdjVMwGPhXAkewLM5hcDS9L/A3nahZtAcekvYF\nzgF+BXhqe/t+khQR90t6HfA24CNthPHWiLhpjbXd0rn+TOB4ST/qrNvc1rv0Ou7o3PbQsuUHGbxT\nWHqsQ5Y91h7A/+ks3965/gCwdztIF9dY+/cj4vBVbr9lxLqDGbw7AaDt5Z0M3l19b/n2EXG5pD8D\n/jPwTEkXAW+LiHvXWK8Vykf4NlYR8T0GH97+KoOj5q4fMhiWR0XEU9vLVBvRALwVOBI4ro0STmLw\nA0HtY38xIl4JHATcCHyovd/9DH6QLDloWGmd698DrujU8NQ2UvmdXXjJtzB4t9B9rP0j4pQhzzvM\nOD7QHvYY3XXfB6aXFtp47Gl03n0tf4yIOC8iXgQcxeC/yRljqNMy88C3jfBbwMsj4sHuyvaI9kPA\nuZJ+FkDSoZ28ez8GPxDukfQzwLuX7ivp6ZJObYfVIwyG/GPtzQvAL0k6XNIBDCKL5brvND4HHCnp\nNEl7tpcXS3rOkG1H+Qpwr6S3S9pH0h6S/p6kF63xsW4Hfn4dzzfMqOc4H3ijpOdLehKDqOzL7Q/n\nnR9MepGk4yXtyeAdyUOkXttuzAPfxi4ivh0RX+uu6lx/B/BN4MuS7mHw4emR7W3nMvga5w+Bq4FL\nOvfdBPwBg6PSOxl8qPhv2ue7FLgA+AbwVQYfoi4/6n18uf184JUMcvZbgdsYfJawV2fbWHbfoY8X\nEY8BpwAzDN7Z/B3w58D+o+7bOhv4ozabP52dBYPIaPn38H99hcfbaTki/jfwx8CFDI72j+CnP2NY\nfv/929dwF7CDwX+PPxlSm+1mlPMPoEjaAfyYwdHDIxFxXLZizMwmXO4PbQOYjYi7MtdhZjbxSoh0\nSvqtRzOziZV74AdwmaRrJL0pcy1mZhMtd6Tzkoi4rf3GxqWSboyIK5dulFTMOVjMzHYnEbFTepL1\nCD8ibmv//TsGv82404e2EZH18u53vzt7DaVc3Av3wr3YPXqxkmwDX9K+kp7SXn8yg6/JXZurHjOz\nSZcz0vk54OL2V+w3A/89Ir6YsZ6hduzYkbuEYrgXiXuRuBdJ6b3INvBjcJKpmZEbZjYzU3yJvXEv\nEvcicS+S0nuR9RevRhmcM6vc+szMSiSJKO1DWzMz648H/ghN0+QuoRjuReJeJO5FUnovPPDNzCrh\nDN/MbMI4wzczq5wH/gilZ3J9ci8S9yJxL5LSe+GBb2ZWCWf4ZmYTxhm+mVnlPPBHKD2T65N7kbgX\niXuRlN4LD3wzs0o4wzczmzDO8M3MKueBP0LpmVyf3IvEvUjci6T0Xnjgm5lVwhm+mdmEcYZvZlY5\nD/wRSs/k+uReJO5F4l4kpfci5x8xt561fzA+O8d0Znk4w6/IYODn7qc88M02mDN8M7PKeeCPUHom\n168mdwHF8H6RuBdJ6b3IPvAl7SFpm6QtuWsxM5tk2TN8SacDLwSeEhGvXnabM/wxcoZvVociM3xJ\nhwH/EPgwUMZXSMzMJlTuSOcc4AxgMXMdKyo9k+tXk7uAYni/SNyLpPReZPsevqRTgDsiYpuk2ZW2\nm5ubY3p6GoCpqSlmZmaYnR1svtTcjVxeWFjo9fk2cnmgAWY711nH8sI6t19pmTXV6+XdY3lJKfXk\nXM41L5qmYX5+HuDxeTlMtgxf0vuA3wAeBfYG9gcujIjf7GzjDH+MnOGb1WGlDD/7h7YAkk4C3hYR\nr1q23gN/jDzwzepQ5Ie2yxQ5BZa/ba1bk7uAYni/SNyLpPReFHEunYi4Argidx1mZpOsiEhnJY50\nxsuRjlkddodIx8zMNpAH/gilZ3L9anIXUAzvF4l7kZTeCw98M7NKOMOviDN8szo4wzczq5wH/gil\nZ3L9anIXUAzvF4l7kZTeCw98M7NKOMOviDN8szo4wzczq5wH/gilZ3L9anIXUAzvF4l7kZTeCw98\nM7NKOMOviDN8szo4wzczq5wH/gilZ3L9anIXUAzvF4l7kZTeCw98M7NKOMOviDN8szo4wzczq5wH\n/gilZ3L9anIXUAzvF4l7kZTeCw98M7NKOMOviDN8szo4wzczq5wH/gilZ3L9anIXUAzvF4l7kZTe\ni2wDX9LekrZKWpB0naSzctViZlaDrBm+pH0j4gFJm4GrgLdExNbO7c7wx8gZvlkdiszwI+KB9upe\nwJ7AYsZyzMwmWtaBL2mTpAXgduCLEfHVnPUMU3om168mdwHF8H6RuBdJ6b3YnPPJI2IRmJF0AHCx\npKMj4vruNnNzc0xPTwMwNTXFzMwMs7OzQGruRi4vLCz0+nwbuTzQALOd66xjeWGd26+0zJrq9fLu\nsbyklHpyLueaF03TMD8/D/D4vBymmO/hS/pj4IGI+A+ddc7wx8gZvlkdisvwJR0oaaq9vg/wCmB7\nrnrMzCZdzgz/YOCvJX0d+AqDDP/zGesZavnb1ro1uQsohveLxL1ISu9Ftgw/Iq4FXpDr+c3MalNM\nhj+MM/zxcoZvVofiMnwzM+uXB/4IpWdy/WpyF1AM7xeJe5GU3gsPfDOzSjjDr4gzfLM6OMM3M6uc\nB/4IpWdy/WpyF1AM7xeJe5GU3gsPfDOzSjjDr4gzfLM6OMM3M6ucB/4IpWdy/WpyF1AM7xeJe5GU\n3gsPfDOzSjjDr4gzfLM6OMM3M6ucB/4IpWdy/WpyF1AM7xeJe5GU3gsPfDOzSjjDr4gzfLM6OMM3\nM6ucB/4IpWdy/WpyF1AM7xeJe5GU3gsPfDOzSjjDr4gzfLM6OMM3M6ucB/4IpWdy/WpyF1AM7xeJ\ne5GU3otsA1/S4ZIul3S9pOskvTlXLWZmNciW4Us6CDgoIhYk7Qf8DfCaiNje2cYZ/hg5wzerQ3EZ\nfkT8ICIW2uv3AduBQ3LVY2Y26YrI8CVNA8cCW/NWsrPSM7l+NbkLKIb3i8S9SErvxebcBbRxzqeB\nt7RH+j9lbm6O6elpAKamppiZmWF2dhZIzd3I5YWFhV6fbyOXBxpgtnOddSwvrHP7lZZZU71e3j2W\nl5RST87lXPOiaRrm5+cBHp+Xw2T9Hr6kPYHPAZdExLlDbneGP0bO8M3qsFKGn/NDWwEfBe6MiD9Y\nYRsP/DHywDerQ3Ef2gIvAU4DXiZpW3s5OWM9Qy1/21q3JncBxfB+kbgXSem9yJbhR8RVFPKhsZlZ\nDVaNdCRtAk6IiKv7K+mnnt+Rzhg50jGrwy5FOhGxCHxww6oyM7PerCVSuUzSP2o/ZK1O6Zlcv5rc\nBRTD+0XiXiSl92ItA/9fA58CfiLp3vby4w2uy8zMxsznw6+IM3yzOqyU4a/pWzqSTgV+icG0uCIi\ntoy5PjMz22AjIx1J7wfeDFzP4ARnb5Z09kYXVorSM7l+NbkLKIb3i8S9SErvxVqO8H8NmImIxwAk\nzTM4qco7N7AuMzMbs5EZvqRvAC+LiDvb5acBl0fE8za8OGf4Y+UM36wOTyTDPxv4mqTLAQEnAX84\n5vrMzGyDrZrht79puwicCFwMXAicGBGf7KG2IpSeyfWryV1AMbxfJO5FUnovVj3Cj4hFSW+PiAuA\nz/RUk5mZbYC1ZPjvB34IXADcv7Q+Iu7a2NKc4Y+bM3yzOuzy+fAl7WDnKRER8azxlbfic3vgj5EH\nvlkddunkaW2G/46IOGLZZcOHfSlKz+T61eQuoBjeLxL3Iim9F2s5W+bbe6rFzMw2kDP8ijjSMavD\nuDN8IuKIsVW38nN74I+RB75ZHXb5b9pGxPSQDH/Dh/24SCriMhma3AUUo/Sstk/uRVJ6L1Yc+JLe\n3rn+2mW3vW8jixq/eAKXy5/g/X00a2ZlWDHSkbQtIo5dfn3Y8oYVN4ZIxzFGpwr3wqwKuxzpmJnZ\nZPDAH6nJXUBBmtwFFKP0rLZP7kVSei9WO5fO8yTd217fp3MdYJ9xPLmkv2Bwvv07IuKYcTymmZkN\nl/Vv2kp6KXAf8LFhA98Z/pircC/MqlBkhh8RVwI/ylmDmVktnOGP1OQuoCBN7gKKUXpW2yf3Iim9\nF2v5i1dZzc3NMT09DcDU1BQzMzPMzs4CqbmjlpOl5dl1LC+sc/thy6yr3o1aTjXt6utZWOf2Ky2z\npnq9vHssLymlnpzLCwsLWZ6/aRrm5+cBHp+Xw2TN8AEkTQNbnOH3UIV7YVaFIjN8MzPrT9aBL+l8\n4GrgSEm3SHpjznqGa3IXUJAmdwHF2DkurJd7kZTei6wZfkS8Pufzm5nVJHuGvxpn+GOuwr0wq4Iz\nfDOzynngj9TkLqAgTe4CilF6Vtsn9yIpvRce+GZmlXCG34sycmv3wqwOzvDNzCrngT9Sk7uAgjS5\nCyhG6Vltn9yLpPReeOCbmVXCGX4vysit3QuzOjjDNzOrnAf+SE3uAgrS5C6gGKVntX1yL5LSe+GB\nb2ZWCWf4vSgjt3YvzOrgDN/MrHIe+CM1uQsoSJO7gGKUntX2yb1ISu+FB76ZWSWc4feijNzavTCr\ngzN8M7PKeeCP1OQuoCBN7gKKUXpW2yf3Iim9Fx74ZmaVcIbfizJya/fCrA7O8M3MKueBP1KTu4CC\nNLkLKEbpWW2f3Iuk9F5kHfiSTpZ0o6SbJb0jZy1mZpMuW4YvaQ/gJuAfALcCXwVeHxHbO9s4wx9n\nFe6FWRVKzPCPA74ZETsi4hHgk8CpGesxM5toOQf+ocAtneW/bdcVpsldQEGa3AUUo/Ssdj0kFXGZ\nBKXvF5szPvea3tfPzc0xPT0NwNTUFDMzM8zOzgKpuaOWoYydaa31btTyQBm9gLz9OOuss3jPe94z\nnheyi97whjcwNzeXbX8obTjl/v+jpB86EbGu+pumYX5+HuDxeTlMzgz/BOCsiDi5XX4nsBgR/76z\nzRPO8M3M1qKMz7hgHJ9zlZjhXwM8W9K0pL2A1wGfzViPmdlEyzbwI+JR4HeB/wXcAFzQ/YZOKUp7\n25uTe5G4F4l70dXkLmBVOTN8IuIS4JKcNZiZ1WLiz6VjZrYWzvDNzGxieOCP4HwycS8S9yJxL7qa\n3AWsygPfzKwSzvDNzHCGb2ZmE8QDfwTnk4l7kbgXiXvR1eQuYFUe+GZmlXCGb2aGM3wzM5sgHvgj\nOJ9M3IvEvUjci64mdwGr8sA3M6uEM3wzM5zhm5nZBPHAH8H5ZOJeJO5F4l50NbkLWJUHvplZJZzh\nm5nhDN/MzCaIB/4IzicT9yJxLxL3oqvJXcCqPPDNzCrhDN/MDGf4ZmY2QTzwR3A+mbgXiXuRuBdd\nTe4CVpVl4Et6raTrJT0m6QU5ajAzq02WDF/Sc4BF4L8Bb42Ir62wnTN8M+tFDRn+5if0qLsoIm6E\npQabmVkfnOGP4HwycS8S9yJxL7qa3AWsasOO8CVdChw05KZ3RcSWtT7O3Nwc09PTAExNTTEzM8Ps\n7CyQdrSNXF5YWOj1+UpeXlhYKKoeL5exvKSUenZ1uX0VwGznOutcXniC9+9Uso76m6Zhfn4e4PF5\nOUzW7+FLuhxn+GZWgBoy/BIiHQf5ZmY9yPW1zF+XdAtwAvA/JV2So461WP62tWbuReJeJO5FV5O7\ngFXl+pbOxcDFOZ7bzKxWPpeOmRnO8M3MbIJ44I/gfDJxLxL3InEvuprcBazKA9/MrBLO8M3McIZv\nZmYTxAN/BOeTiXuRuBeJe9HV5C5gVR74ZmaVcIZvZoYzfDMzmyAe+CM4n0zci8S9SNyLriZ3Aavy\nwDczq4QzfDMznOGbmdkE8cAfwflk4l4k7kXiXnQ1uQtYlQe+mVklnOGbmeEM38zMJogH/gjOJxP3\nInEvEveiq8ldwKqy/E1bM7My7ZSCTBRn+GZmE8YZvplZ5bIMfEl/Imm7pK9LukjSATnqWAvnk4l7\nkbgXiXuRlN6LXEf4XwSOjojnA/8PeGemOkZaWFjIXUIx3IvEvUjci6T0XmQZ+BFxaUQstotbgcNy\n1LEWd999d+4SiuFeJO5F4l4kpfeihAz/nwOfz12Emdmk27CvZUq6FDhoyE3viogt7TZnAj+JiL/a\nqDqeqB07duQuoRjuReJeJO5FUnovsn0tU9Ic8CbglyPioRW28Xcyzcx2wbCvZWb5xStJJwNnACet\nNOxheMFmZrZrshzhS7oZ2Au4q131pYj47d4LMTOrSNG/aWtmZuPjc+l0SHoucCpwaLvqb4HPRsT2\nfFVZbu1+cQiwNSLu66w/OSK+kK+y/kk6HliMiK9KOho4GdgeEf6m3W6ghK9lFkHSO4Dz28Wt7WUT\ncL6kYn8xrG+S3pi7hj5JejPwP4DfA66X9JrOzWfnqSoPSWcBfwr8V0lnA+cB+wJ/KOmPctZWAkkf\ny13DKI50Wu3nCkdFxCPL1u8F3BARv5CnsrJIuiUiDs9dR18kXQecEBH3SZoGPg18IiLOlbQtIo7N\nWmCP2l7MMPj87XbgsIi4R9I+wFci4pisBfZI0hYGfy2l+8WSlwN/DUREvDpLYSM40kkeYxDl7Fi2\n/pD2tmpIunaVm5/eWyFl0FKMExE7JM0CF0p6JpN+Lt2dPRoRjwKPSvpWRNwDEBEPSloccd9Jcxhw\nA/BhYJHBvvAi4AM5ixrFAz/5feAySd8EbmnXHQ48G/jdbFXl8XQG2eyPhtx2dc+15HaHpJmIWABo\nj/RPAT4CPC9vab17WNK+EfEA8IKllZKmqOygiMFwfwtwJnBGRGyT9FBEXJG5rlU50umQtAdwHIMj\n/QBuBa5pj2qqIekvgL+MiCuH3HZ+RLw+Q1lZSDoceCQifrBsvYCXRMRVeSrrn6S9h/3ejKQDgYMj\nYrV3hhNJ0mHAOcAdwKtLjzs98M3MnqD2Xd8vRsS7cteyGg98M7NK+GuZZmaV8MA3M6uEB76ZWSU8\n8K1aks6UdF37t5W3SToud01mG8nfw7cqSToR+DXg2Ih4RNLPAE/q8fkFg1/J7Os5zXyEb7U6CPjh\n0qk0IuKuiLhN0gslNZKukfQFSQcBtOveL2mrpJsk/f12/dHtum3tO4Wfb9efLuna9vKWdt10e9+P\nAtdS8N9ytsnkr2ValSQ9GbiKwcm/LgMuAL4EXAG8KiLulPQ64JUR8VuSLmfwS3hnSPpV4PSIeIWk\n8xj8PYe/krSZwbvmo4G/BI5ncFC1FTgNuBv4FnBiRHyl1xdshiMdq1RE3C/phcBLgZcxGPjvZTCs\nL2sTlz2A73fudlH779eA6fb61cCZ7W9cXhQR32yP/i+KiAcBJF3UPs9nge962FsuHvhWrYhYZHBE\nf0V7wrjfAa6PiF9c4S4Pt/8+Rvv/TkScL+nLwCnA5yX9K3Y+i6LadQD3j/dVmK2dM3yrkqQjJT27\ns+pYYDtwoKQT2m32lHTUiMd5VkR8JyLOAz4DHANcCbxG0j5tdPSadl1tZ9e0wvgI32q1H3Bee6bH\nR4GbgX8J/DnwnyQdwOD/j3MYnAZ3uaUj9n8s6TTgEeA24N9FxN2S5oGl6OZDEfH19nz6/tDMsvGH\ntmZmlXCkY2ZWCQ98M7NKeOCbmVXCA9/MrBIe+GZmlfDANzOrhAe+mVkl/j+ofd2HdK7JZAAAAABJ\nRU5ErkJggg==\n",
      "text/plain": [
       "<matplotlib.figure.Figure at 0x10c4ba790>"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "import pandas as pd\n",
    "from IPython.display import display\n",
    "import matplotlib.pyplot as plt\n",
    "%matplotlib inline\n",
    "\n",
    "me = pd.read_csv(\"MeasurementError.csv\")\n",
    "display(me)\n",
    "me[\"Error\"].plot(kind=\"bar\")\n",
    "\n",
    "plt.title('Measurement Errors')\n",
    "plt.xlabel('Sensor')\n",
    "plt.ylabel('Error')\n",
    "plt.grid()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": false
   },
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Python 2",
   "language": "python",
   "name": "python2"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 2
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython2",
   "version": "2.7.10"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
